target C {
  keepalive: true
}

import SimpleWebSocketServer from "lib/SimpleWebSocketServer.lf"

reactor AccountManager {
  preamble {=
    #include <stdlib.h> // For asprintf
    #include <string.h> // For strlen
  =}
  input money: char*
  output response: char*
  state balance: int = 0

  reaction(money) -> response {=
    int received = atoi(money->value); // Will report 0 if not a valid int.
    lf_print("Received %d", received);
    self->balance += received;
    char* balance;
    asprintf(&balance, "%d", self->balance);
    lf_set_array(response, balance, strlen(balance));
    lf_print("**** Balance: %s", balance);
  =}
}

main reactor {
  w = new SimpleWebSocketServer(
      initial_file = {= LF_SOURCE_DIRECTORY LF_FILE_SEPARATOR "Bank1.html" =})
  a = new AccountManager()
  w.received -> a.money
  a.response -> w.in
}
