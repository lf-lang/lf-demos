target C

/**
 * Starting at time `offset`, generate a periodic output for a given number of repetitions.
 * The value of the output is a string representation of the number given by `value`.
 */
reactor FinitePeriodicSource(offset:time = 1 s, period:time = 1 s, value:int = 0, repetitions:int = 4) {
  preamble {=
    #include <stdlib.h> // For asprintf
    #include <string.h> // For strlen
  =}
  output out:char*
  logical action a
  state count:int = 0
  reaction(startup) -> a {=
    lf_schedule(a, self->offset);
  =}
  reaction(a) -> out, a {=
    char* message;
    asprintf(&message, "%d", self->value);
    lf_set_array(out, message, strlen(message));
    if (self->count++ < self->repetitions - 1) {
      lf_schedule(a, self->period);
    }
  =}
}
